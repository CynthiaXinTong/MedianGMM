model {
      for (i in 1:N)  {
           mem[i] ~ dcat(lambda[1:3])

          for(t in 1:Time) {
               V[i,t] ~ dexp(pre_sigma[mem[i]])
               y[i,t] ~ dnorm(muy[i,t], pre_sig2[i,t])
               muy[i,t] <- LS[i,1]+(t-1)*LS[i,2] + zeta*V[i,t]
               pre_sig2[i,t]<- 1/sig2_y[i,t]
               sig2_y[i,t] <- eta^2*V[i,t]/pre_sigma[mem[i]]


               #log-likelihood
               loglik[i,t] <- logdensity.norm(y[i,t], muy[i,t], pre_sig2[i,t])
			
          }

        LS[i,1:2]  ~ dmnorm(muLS[mem[i],1:2], Inv_cov[mem[i],1:2,1:2])

      }
      zeta <- (1-2*tau)/(tau*(1-tau))
      eta <- sqrt(2/(tau*(1-tau)))

      ########################
      # prior specification
      ########################

      # --------- sigma ---------
      pre_sigma[1] ~ dgamma(.001, .001)
      pre_sigma[2] ~ dgamma(.001, .001)
      pre_sigma[3] ~ dgamma(.001, .001)
      sigma[1] <- 1/pre_sigma[1]
      sigma[2] <- 1/pre_sigma[2]
      sigma[3] <- 1/pre_sigma[3]

      # --------- muLS ---------
      # Intercept
      muLS[1,1] ~ dnorm(0, 0.001)I(muLS[2,1],)
      muLS[2,1] ~ dnorm(0, 0.001)
      muLS[3,1] ~ dnorm(0, 0.001)
      # Slope
      muLS[1,2] ~ dnorm(0, 0.001)
      muLS[2,2] ~ dnorm(0, 0.001)
      muLS[3,2] ~ dnorm(0, 0.001)

      # --------- Inv_cov ---------
      Inv_cov[1,1:2,1:2] ~ dwish(R[1:2,1:2],2)
      Inv_cov[2,1:2,1:2] ~ dwish(R[1:2,1:2],2)
      Inv_cov[3,1:2,1:2] ~ dwish(R[1:2,1:2],2)

      Cov_b[1,1:2,1:2] <- inverse(Inv_cov[1,1:2,1:2])
      Cov_b[2,1:2,1:2] <- inverse(Inv_cov[2,1:2,1:2])
      Cov_b[3,1:2,1:2] <- inverse(Inv_cov[3,1:2,1:2])
      R[1,1]<-1
      R[2,2]<-1
      R[2,1]<-R[1,2]
      R[1,2]<-0

      # --------- lambda ---------
	  lambda[1:3] ~ ddirich(alpha[1:3])

      # --------- Organizing parameters ---------

      par[1]   <- muLS[1,1]
      par[2]   <- muLS[1,2]
      par[3]   <- Cov_b[1,1,1]
      par[4]   <- Cov_b[1,1,2]
      par[5]   <- Cov_b[1,2,2]
      par[6]   <- muLS[2,1]
      par[7]   <- muLS[2,2]
      par[8]   <- Cov_b[2,1,1]
      par[9]   <- Cov_b[2,1,2]
      par[10]   <- Cov_b[2,2,2]
      par[11]   <- muLS[3,1]
      par[12]   <- muLS[3,2]
      par[13]   <- Cov_b[3,1,1]
      par[14]   <- Cov_b[3,1,2]
      par[15]   <- Cov_b[3,2,2]
      par[16]  <- lambda[1]
      par[17]  <- lambda[2]
      par[18]  <- lambda[3]
      par[19]  <- sigma[1]
      par[20]  <- sigma[2]
      par[21]  <- sigma[3]

} # model
