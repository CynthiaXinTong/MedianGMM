model {
      for (i in 1:N)  {
           mem[i] ~ dcat(lambda[1:2])

          for(t in 1:Time) {
               y[i,t] ~ dnorm(muy[i,t], pre_sig2[mem[i]])
               muy[i,t] <- LS[i,1]+(t-1)*LS[i,2]
               #log-likelihood
               loglik[i,t] <- logdensity.norm(y[i,t], muy[i,t], pre_sig2[mem[i]])
			
          } 

        LS[i,1:2]  ~ dmnorm(muLS[mem[i],1:2], Inv_cov[mem[i],1:2,1:2])

      } 

      ########################
      # prior specification
      ########################

      # --------- pre_sig2 ---------
      pre_sig2[1] ~ dgamma(0.001, 0.001)
      pre_sig2[2] ~ dgamma(0.001, 0.001)
      sig2[1] <- 1/pre_sig2[1]
      sig2[2] <- 1/pre_sig2[2]

      # --------- muLS ---------
      # Intercept
      muLS[1,1] ~ dnorm(0, 0.001)I(muLS[2,1],)
      muLS[2,1] ~ dnorm(0, 0.001)
      # Slope
      muLS[1,2] ~ dnorm(0, 0.001)
      muLS[2,2] ~ dnorm(0, 0.001)

      # --------- Inv_cov ---------
      Inv_cov[1,1:2,1:2] ~ dwish(R[1:2,1:2],2)
      Inv_cov[2,1:2,1:2] ~ dwish(R[1:2,1:2],2)

      Cov_b[1,1:2,1:2] <- inverse(Inv_cov[1,1:2,1:2])
      Cov_b[2,1:2,1:2] <- inverse(Inv_cov[2,1:2,1:2])
      R[1,1]<-1
      R[2,2]<-1
      R[2,1]<-R[1,2]
      R[1,2]<-0

      # --------- lambda ---------
	  lambda[1:2] ~ ddirich(alpha[1:2])

      ## --- Reorganize model parameters

      par[1]   <- muLS[1,1]
      par[2]   <- muLS[1,2]
      par[3]   <- Cov_b[1,1,1]
      par[4]   <- Cov_b[1,1,2]
      par[5]   <- Cov_b[1,2,2]
      par[6]   <- muLS[2,1]
      par[7]   <- muLS[2,2]
      par[8]   <- Cov_b[2,1,1]
      par[9]   <- Cov_b[2,1,2]
      par[10]  <- Cov_b[2,2,2]
      par[11]  <- lambda[1]
      par[12]  <- lambda[2]
      par[13]  <- sig2[1]
      par[14]  <- sig2[2]

} # model
